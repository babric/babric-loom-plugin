plugins {
    id("java")
    id("maven-publish")
    id("java-gradle-plugin")
    id("idea")
}

group = "babric"
base.archivesName = project.name
def baseVersion = "1.8"

def ENV = System.getenv()

def loomVersion = baseVersion + "-SNAPSHOT"

if (ENV.EXPERIMENTAL) {
    loomVersion = "1.8.0-alpha.16"
}

if (ENV.containsKey("GITHUB_RUN_NUMBER")) {
    if (ENV.EXPERIMENTAL) {
        version = baseVersion + ".0-alpha." + ENV["GITHUB_RUN_NUMBER"]
    } else {
        version = baseVersion + "." + ENV["GITHUB_RUN_NUMBER"]
    }
} else {
    version= "${baseVersion}.local"
}

repositories {
    maven {
        name = "FabricMC"
        url = "https://maven.fabricmc.net/"
    }
    maven {
        name = 'Babric'
        url = 'https://maven.glass-launcher.net/babric'
    }
    mavenCentral()
}

dependencies {
    implementation(gradleApi())

    implementation("net.fabricmc:fabric-loom:${loomVersion}")
    compileOnly("net.fabricmc:mapping-io:0.6.1")
    api("babric:stitch:0.6.2-babric.1")

    testImplementation(platform("org.junit:junit-bom:5.9.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")
}

tasks.test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Version': project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
}

gradlePlugin {
    plugins {
        babricLoomExtension {
            id = 'babric-loom-extension'
            implementationClass = 'babric.BabricLoomPlugin'
        }
    }
}

import org.w3c.dom.Document
import org.w3c.dom.Element
import org.w3c.dom.Node

publishing {
    publications {
        if (!ENV.containsKey("EXPERIMENTAL") && !ENV.EXPERIMENTAL) {
            // Also publish a snapshot so people can use the latest version if they wish
            snapshot(MavenPublication) { publication ->
                groupId project.group
                artifactId project.base.archivesName.get()
                version baseVersion + '-SNAPSHOT'

                from components.java
            }

            // Manually crate the plugin marker for snapshot versions
            snapshotPlugin(MavenPublication) { publication ->
                groupId 'babric-loom-extension'
                artifactId 'babric-loom-extension.gradle.plugin'
                version baseVersion + '-SNAPSHOT'

                pom.withXml({
                    // Based off org.gradle.plugin.devel.plugins.MavenPluginPublishPlugin
                    Element root = asElement()
                    Document document = root.getOwnerDocument()
                    Node dependencies = root.appendChild(document.createElement('dependencies'))
                    Node dependency = dependencies.appendChild(document.createElement('dependency'))
                    Node groupId = dependency.appendChild(document.createElement('groupId'))
                    groupId.setTextContent('babric')
                    Node artifactId = dependency.appendChild(document.createElement('artifactId'))
                    artifactId.setTextContent('babric-loom-extension')
                    Node version = dependency.appendChild(document.createElement('version'))
                    version.setTextContent(baseVersion + '-SNAPSHOT')
                })
            }
        }
    }
    repositories {
        maven {
            if (ENV.MAVEN_URL) {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}
